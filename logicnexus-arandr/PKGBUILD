# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Cl√©ment DEMOULINS <clement@archivel.fr>
# Maintainer: Aenosh Rajora <aenoshrajora79@gmail.com>

pkgname=logicnexus-arandr
_pkgname=arandr
pkgver=0.1.11
pkgrel=2
pkgdesc="Provide a simple visual front end for XRandR 1.2, Modified for logicnexus"
arch=('any')
url="https://christian.amsuess.com/tools/arandr/"
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-gobject' 'xorg-xrandr' 'logicnexus-scripts')
makedepends=('python-docutils')
source=(https://christian.amsuess.com/tools/$_pkgname/files/${_pkgname}-$pkgver.tar.gz
        dont-compress-man-pages.patch
        xrandr-logicnexus.patch)
sha256sums=('e4cbbe3698bb812b395770870174be0094bbaeb391105a811f95f42eb182ae02'
            'eb954b763bd2dd7f98ff04a4d45e59db9f8a16cc7384b47ffed733781057ab37'
            '2c9873f29c5821837aad7e9e780a92624bdf890db797444936eafa0a86f26e08')
conflicts=('arandr')

prepare() {
  cd "$srcdir"/$_pkgname-$pkgver
  # Don't create gzip files with a build date in them which makes it unreproducible
  patch -Np1 -i ${srcdir}/dont-compress-man-pages.patch

  # Apply logicnexus specific changes
  patch -Np1 -i ${srcdir}/xrandr-logicnexus.patch
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir"
}
